# Windows_3.1_IOServer 1.2 360
set exitcode=0
if ($argv[1] != 0) then
echo Installing Windows 3.1 I/O Server
#installation script
  #argv[1] = 0 for uninstalling software, otherwise the software is installed
  #argv[2] = dirname of the installation disk or tape drive
  #argv[3] = installation medium "tape" or "disk"
  switch ($argv[3])
    case disk:
      echo Loading software from disk ...

      test -d /helios/include
      if ( $status == 1 ) then
        echo Making directory /helios/include
        mkdir /helios/include
      endif

      test -d /helios/users/guest/examples
      if ( $status == 1 ) then
        echo Installing example programs to mswin directory in home directory ...
        cp -r $argv[2]/users/guest/examples/mswin ~/mswin
      else
        echo Installing example programs to ~guest/examples/mswin
        cp -r $argv[2]/users/guest/examples/mswin ~guest/examples/mswin
      endif

      echo
      echo Installing binaries, libraries and include files ...
      cp -r $argv[2]/bin/* /helios/bin 
      if($status != 0) then
        set exitcode=1
      endif

      cp -r $argv[2]/local/bin/* /helios/local/bin 
      if($status != 0) then
        set exitcode=1
      endif

      cp -r $argv[2]/lib/* /helios/lib 
      if($status != 0) then
        set exitcode=1
      endif

      test -f /helios/include/graph.h
      if ($status) then
        cp $argv[2]/include/graph.h /helios/include 
        if($status != 0) then
          set exitcode=1
        endif
      else
        fgrep -si "MS-WINDOWS" /helios/include/graph.h
        if ($status) then
	  echo
          echo Non MS-WINDOWS graph.h found in /helios/include.
	  echo making /helios/include/mswin directory for graph.h.
          echo Modify "#include" directives accordingly when programming
          test -d /helios/include/mswin 
          if ( $status )then
            mkdir /helios/include/mswin
          endif
          cp -r $argv[2]/include/graph.h /helios/include/mswin
        else 
          cp -r $argv[2]/include/graph.h /helios/include
        endif
        if($status != 0) then
          set exitcode=1
        endif
      endif
      echo
      echo When running Helios Windows 3.1 I/O Server you should start
      echo a MSWIN server in the transputer network. This can be done
      echo by adding the following lines to your login file:
      echo
      cat $argv[2]/users/guest/login
      echo Type ""Y"" to add these lines to your login file
      set tempflag=$<
      if ( $tempflag == y || $tempflag == Y ) then
        fgrep -s "startws" ~/login
        if ( $status ) then
          echo Amending logging file ...
	  cat $argv[2]/users/guest/login >> ~/login
        else
          echo Warning startws found in login file, no changes made to login
        endif
      else
        echo Leaving login file unchanged
      endif 

      cp $argv[2]/winsrvr.exe /helios
      if($status != 0) then
        set exitcode=1
      else
        echo _____________________________________________________________
        echo
        echo Helios Windows 3.1 I/O Server is called winsrvr.exe. The server
        echo has been copied to your /helios directory
        echo
        echo Use the Windows 3.1 Program Manager File-New menu option to install
        echo the application as a windows icon
        echo _____________________________________________________________ 
	echo
      endif

      echo Installation complete
      breaksw
    case tape:
       echo No tape support for loadpac yet
       breaksw
    endsw
else
#remove instructions
    echo Deleting Helios Windows 3.1 IOServer

    rm -f /helios/bin/printgrf /helios/bin/printgrfx
    rm -f /helios/winsrvr.exe /helios/include/graph.h /helios/lib/graph.lib
    rm -f /helios/local/bin/startws
    rm -f /helios/include/mswin/graph.h
    rm -rf /helios/users/guest/examples/mswin

    echo Helios Windows 3.1 IOServer Deleted
endif
exit $exitcode
